CLEAR

1 PRINT HEX(03);TAB(28);"HEXAPAWN":PRINT
2 PRINT TAB(11);"Creative Computing  Morristown, New Jersey"
3 PRINT:PRINT:PRINT

10 REM  HEXAPAWN-  Interpretation of hexapawn game as presented in
11 REM  Martin Gardner's "The Unexpected Hanging and Other Mathematical
12 REM  Diversions", chapter eight;  A Matchbox Game-Learning Machine
13 REM  Original version for H-P timeshare system by R.A. Kaapke 5/5/76
14 REM  Instructions by Jeff Dalton
15 REM  Conversion to MITS BASIC by Steve North
16 REM  Conversion to Wang BASIC by Jim Battle

20 DIM B(19,9),M(19,4),S(9),T(9),R(9),P$3,A$1
30 W=0:L=0
40 DEF FNM(Y)=Y-INT(Y/10)*10
45 P$="X.O"
50 MAT READ R, B, M

60 INPUT "Instructions (Y-N)",A$
70 IF A$="N" THEN 90:IF A$="n" THEN 90
72 IF A$="Y" THEN 74:IF A$="y" THEN 74:GOTO 60
74 GOSUB 3000

80 REM Wrap each round in a GOSUB so the RETURN
85 REM will clear any unterminated FOR loops
90 GOSUB 100:GOTO 90

100 X=0:Y=0
111 S(1),S(2),S(3)=-1
112 S(4),S(5),S(6)=0
113 S(7),S(8),S(9)=1
115 GOSUB 2000

120 INPUT "Your move",M1,M2
121 IF M1<>INT(M1) THEN 124:IF M2<>INT(M2) THEN 124
122 IF M1<1 THEN 124:IF M1>9 THEN 124
123 IF M2<1 THEN 124:IF M2>9 THEN 124:GOTO 130
124 PRINT "Illegal co-ordinates."
125 GOTO 120
130 IF S(M1)=1 THEN 150
140 PRINT "Illegal move.": GOTO 120
150 IF S(M2)=1 THEN 140
160 IF M2-M1=-3 THEN 170: IF S(M2)<>-1 THEN 140
170 IF M2>M1 THEN 140
180 IF M2-M1<>-3 THEN 185: IF S(M2)<>0 THEN 140
185 IF M2-M1<-4 THEN 140
186 IF M1<>7 THEN 190:IF M2=3 THEN 140

190 S(M1)=0
200 S(M2)=1
205 GOSUB 2000
210 IF S(1)=1 THEN 830:IF S(2)=1 THEN 830:IF S(3)=1 THEN 830
220 FOR I=1 TO 9:IF S(I)=-1 THEN 230:NEXT I
223 GOTO 830
230 FOR I=1 TO 9
240 IF S(I)<>-1 THEN 330
250 IF S(I+3)=0 THEN 350
260 IF R(I)=I THEN 320
270 IF I>3 THEN 300
280 IF S(5)=1 THEN 350
290 GOTO 330
300 IF S(8)=1 THEN 350
310 GOTO 330
320 IF S(I+2)=1 THEN 350:IF S(I+4)=1 THEN 350
330 NEXT I
340 GOTO 830
350 FOR I=1 TO 19
360 FOR J=1 TO 3
370 FOR K=3 TO 1 STEP -1
380 T((J-1)*3+K)=B(I,(J-1)*3+4-K)
390 NEXT K
400 NEXT J
410 FOR J=1 TO 9
420 IF S(J)<>B(I,J) THEN 460
430 NEXT J
440 R=0
450 GOTO 540
460 FOR J=1 TO 9
470 IF S(J)<>T(J) THEN 510
480 NEXT J
490 R=1
500 GOTO 540
510 NEXT I
520 REMEMBER the termination of this loop is impossible
530 STOP ": ILLEGAL BOARD PATTERN"
540 X=I
550 FOR I=1 TO 4
560 IF M(X,I)<>0 THEN 600
570 NEXT I
580 PRINT "I resign."
590 GOTO 830
600 Y=INT(RND(1)*4+1)
601 IF M(X,Y)=0 THEN 600
610 IF R<>0 THEN 630
620 PRINT "I move from";INT(M(X,Y)/10);"to";FNM(M(X,Y))
622 S(INT(M(X,Y)/10))=0
623 S(FNM(M(X,Y)))=-1
624 GOTO 640
630 PRINT "I move from ";R(INT(M(X,Y)/10));"to";R(FNM(M(X,Y)))
632 S(R(INT(M(X,Y)/10)))=0
633 S(R(FNM(M(X,Y))))=-1
640 GOSUB 2000
641 IF S(7)=-1 THEN 810:IF S(8)=-1 THEN 810:IF S(9)=-1 THEN 810
650 FOR I=1 TO 9
660 IF S(I)=1 THEN 690
670 NEXT I
680 GOTO 810
690 FOR I=1 TO 9
700 IF S(I)<>1 THEN 790
710 IF S(I-3)=0 THEN 120
720 IF R(I)=I THEN 780
730 IF I<7 THEN 760
740 IF S(5)=-1 THEN 120
750 GOTO 790
760 IF S(2)=-1 THEN 120
770 GOTO 790
780 IF S(I-2)=-1 THEN 120:IF S(I-4)=-1 THEN 120
790 NEXT I
800 PRINT "You can't move, so ";
810 PRINT "I win."
815 W=W+1:GOTO 850
830 PRINT "You win."
840 M(X,Y)=0:L=L+1
850 IF L+W<>1 THEN 870:IF W<>1 THEN 860:PRINT "I win the first game.":GOTO 880
860 IF L<>1 THEN 870:PRINT "You win the first game.":GOTO 880
870 PRINT "I have won";W;"and you";L;"out of";L+W;"games."
880 PRINT
890 RETURN:REM back for another round

1000 REM R()
1010 DATA 3, 2, 1, 6, 5, 4, 9, 8, 7

1100 REM B()
1110 DATA -1,-1,-1,1,0,0,0,1,1,-1,-1,-1,0,1,0,1,0,1
1120 DATA -1,0,-1,-1,1,0,0,0,1,0,-1,-1,1,-1,0,0,0,1
1130 DATA -1,0,-1,1,1,0,0,1,0,-1,-1,0,1,0,1,0,0,1
1140 DATA 0,-1,-1,0,-1,1,1,0,0,0,-1,-1,-1,1,1,1,0,0
1150 DATA -1,0,-1,-1,0,1,0,1,0,0,-1,-1,0,1,0,0,0,1
1160 DATA 0,-1,-1,0,1,0,1,0,0,-1,0,-1,1,0,0,0,0,1
1170 DATA 0,0,-1,-1,-1,1,0,0,0,-1,0,0,1,1,1,0,0,0
1180 DATA 0,-1,0,-1,1,1,0,0,0,-1,0,0,-1,-1,1,0,0,0
1190 DATA 0,0,-1,-1,1,0,0,0,0,0,-1,0,1,-1,0,0,0,0
1200 DATA -1,0,0,-1,1,0,0,0,0

1300 REM M()
1310 DATA 24,25,36,0,14,15,36,0,15,35,36,47,36,58,59,0
1320 DATA 15,35,36,0,24,25,26,0,26,57,58,0
1330 DATA 26,35,0,0,47,48,0,0,35,36,0,0,35,36,0,0
1340 DATA 36,0,0,0,47,58,0,0,15,0,0,0
1350 DATA 26,47,0,0,47,58,0,0,35,36,47,0,28,58,0,0,15,47,0,0

2000 PRINT:REM PRINT THE BOARD
2010 FOR I=1 TO 3
2020 FOR J=1 TO 3
2030 PRINT TAB(10);STR(P$,S((I-1)*3+J)+2,1);
2040 NEXT J
2050 PRINT
2060 NEXT I
2070 PRINT
2080 RETURN

3000 PRINT:PRINT "This program plays the game of HEXAPAWN.":PRINT
3010 PRINT "HEXAPAWN is played with chess pawns on a 3 by 3 board."
3020 PRINT "The pawns are moved as in chess - one space forward to"
3030 PRINT "an empty space or one space forward and diagonally to"
3040 PRINT "capture an opposing man.  On the board, your pawns"
3050 PRINT "are 'O', the computer's pawns are 'X', and empty "
3060 PRINT "squares are '.'.  To enter a move, type the number of"
3070 PRINT "the square you are moving FROM, followed by the number"
3080 PRINT "of the square you will move TO.  The numbers must be"
3090 PRINT "separated by a comma.":PRINT
3100 GOSUB 4000
3110 PRINT "The computer starts a series of games knowing only when"
3120 PRINT "the game is won (a draw is impossible) and how to move."
3130 PRINT "It has no strategy at first and just moves randomly."
3140 PRINT "However, it learns from each game.  Thus, winning becomes"
3150 PRINT "more and more difficult.  Also, to help offset your"
3160 PRINT "initial advantage, you will not be told how to win the"
3170 PRINT "game but must learn this by playing.":PRINT
3180 GOSUB 4000
3190 PRINT "The numbering of the board is as follows:"
3220 PRINT TAB(10);"123":PRINT TAB(10);"456":PRINT TAB(10);"789"
3230 PRINT:PRINT "For example, to move your rightmost pawn forward,"
3230 PRINT "you would type 9,6 in response to the question"
3240 PRINT "'Your move '.  Since I'm a good sport, you'll always"
3250 PRINT "go first.":PRINT
3260 RETURN

4000 REM wait for any keypress
4010 PRINT "(press any key to continue)";
4020 KEYIN A$,4030,4030:GOTO 4020
4030 PRINT:PRINT:RETURN

9999 END
